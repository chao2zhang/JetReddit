plugins {
  id "io.gitlab.arturbosch.detekt" version "1.19.0"
  id "com.android.application"
  id "kotlin-android"
  id "kotlin-kapt"
  id "dagger.hilt.android.plugin"
}

android {
  compileSdkVersion 31
  buildToolsVersion "30.0.3"

  defaultConfig {
    applicationId "com.chao2zhang.jetreddit"
    minSdkVersion 26
    targetSdkVersion 31
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildFeatures {
    compose true
    viewBinding true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = '1.8'
  }

  buildFeatures {
    compose true
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    freeCompilerArgs += '-Xopt-in=kotlin.Experimental'
  }
}

detekt {
  buildUponDefaultConfig = true
  config = files("$projectDir/config/detekt.yml")
  reports {
    html.enabled = true
    xml.enabled = false
    txt.enabled = false
    sarif.enabled = true
  }
}

tasks.detekt.jvmTarget = "1.8"

dependencies {
  // Kotlin
  implementation libs.kotlin.stdlib
  implementation libs.kotlinx.coroutines
  testImplementation libs.kotlinx.coroutinesTest

  // Android
  implementation libs.androidx.core
  implementation libs.androidx.appcompat
  implementation libs.androidx.navigationFragmentKtx
  implementation libs.androidx.navigationUiKtx
  implementation libs.androidx.viewModelKtx
  implementation libs.androidx.liveDataKtx

  // Compose
  implementation libs.bundles.compose

  // Networking
  implementation libs.bundles.retrofit

  // Image
  implementation libs.coil.compose

  // Dagger
  implementation libs.hilt.android
  kapt libs.hilt.compiler

  // WorkManager
  implementation libs.androidx.work

  // Test
  testImplementation libs.junit
  testImplementation libs.androidx.coreTesting
  androidTestImplementation libs.androidx.testExt
  androidTestImplementation libs.androidx.espresso
}

kapt {
  correctErrorTypes true
}